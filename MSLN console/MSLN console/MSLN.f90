    subroutine msln(ip,aa,aw,s,dL,dC)
! MSLN - Microstrip Coupled Lines, N-strip 
! МСМПЛ - Многопроводные связанные микрополосковые линии
    implicit complex*16(c,w,z), real*8(a-b,d-h,o-v,x-y)
    dimension betam(82),qwork(2004)			! nptsq*(2n+3)=6*(2*82+3)=1002
    dimension aa(5), aw(9), s(8)			! Nmax=9  NNmax=8*Nmax+2=74
    dimension z1(74),z2(74),z3(74), x3(74),x4(74), x5(36), aC(289) ! 8n+2   !(2n-1)**2
    dimension aws(20), xs(20), bC(81), dC(81), dCC(81), dL(81)
! Кол-во проводников "КПЛ-структуры" n2 из-за разрез. больше кол-ва линий n, т.е. n2 = 2n-1. 
!----------------------------------------------------------------------------------------
	zinf = (1.d20,1.d20)      ! "машинная" бесконечность
	zero = (0.d0, 0.d0)		  ! нуль
!	zr = (1.d0, 0.d0)		  ! вещест.единица  
	zi = (0.d0, 1.d0)		  ! мнимая единица	
    pi = dacos(-1.d0)		  ! число пи
!----------------------------------------------------------------------------------------
!	ip=0   ! Индекс печати = -2,-1,0,1. Если ip=0, всё печат.  
!			 ip=iprint					Если ip=-2, ничего не печат.
!	Исходные данные:
	n  = aa(1)	! количество микрополосквых линий передачи n 
    h  = aa(2)	! толщина диэлектрической подложки h1(мм)
	t  = aa(3)	! толщина микрополосковых линий t(мм)
    e1 = aa(4)	! диэл.проницаем. диэлектрической подложки e1
	e2 = aa(5)	! диэл.проницаем. верхней окруж.среды e2
!	aw = aw		! ширины полосок (линий) на подложке w(мм)
!	s  = s		! зазоры между микрополосками s(мм)
!==============================================================================
    nn = 8*n+2	! Общее кол-во вершин в исх. многоугол.(вкл. нуль и беcконеч.)
    do 10 i =1,n		! Формиров. угловых точек геометрии поперч.сечения МПЛ
        i8=8*(i-1)
        sw=aw(i)/2. 
     do 9 j = 1,i-1
9    sw=sw+aw(j)+s(j) ! Текущие координаты оснований "ножек грибов"
        z1(i8+1) = sw;					betam(i8+1) = -0.5	! beta = alpha - 1	
        z1(i8+2) = z1(i8+1) + zi*h;		betam(i8+2) = -0.5
        z1(i8+3) = z1(i8+2) - aw(i)/2;  betam(i8+3) =  0.5
        z1(i8+4) = z1(i8+3) + zi*t;     betam(i8+4) =  0.5
        z1(i8+5) = z1(i8+4) + aw(i);	betam(i8+5) =  0.5
        z1(i8+6) = z1(i8+5) - zi*t;		betam(i8+6) =  0.5
        z1(i8+7) = z1(i8+2);			betam(i8+7) = -0.5
        z1(i8+8) = z1(i8+1);			betam(i8+8) = -0.5
10  continue   
    sww=sw+aw(n)/2           ! Сумма всех ширин полосок и зазоров между ними
    z1(8*n+1) = zinf		 ! Машинная бесконечность (предпоследняя точка)
    z1(8*n+2) = -10000.		 ! Послед.точка (м.б. нулём). Найдена числен.экспер.
    if(n<4) z1(8*n+2) =-100. ! Послед.точка (м.б. нулём). Найдена числен.экспер.
    betam(8*n+1) = -2.		 ! Угол в бесконечности =-180 град.
    betam(8*n+2) =  0.		 ! Угол в точке на оси 0Х =180 град.
!-------------------------------------------------------------------------------
! Контрольная печать исходных данных (коорд.многоугольника)
   	if(ip==0) then
		print '('' nn ='', i2)', nn
		print*,'MSLN geometry'			   ! write(*,'('' MSLN geometry'')')			
	  do i = 1,nn; 
	   print '(''  z1= w(k)='', i3, 2x, f8.3, 1x, f6.3)', i, z1(i); end do
!	  write(*,'(''  z1= w(k)='', i3, 2x, f8.3, 1x, f6.3)') i, z1(i); end do
	end if
!==============================================================================
! Задаём конформ. центра (z10 = WC) в исх. многоугол. z1
    z10 = dcmplx(sww/2, sww/8+(h+t))       ! Найдено эвристич./эмпирич.
	if(ip==0) print '(''z10= wc = ('', 2e13.5, '')'' )', z10

! Вычисляем узлы и веса для параметрической задачи (влияет на точность):
    nq = 10				! обычно nq = nptsq = 2..6, но для МПЛ nq=8 точнее!!!
    call qinit(nn,betam,nq,qwork)

! Начальное приближение для отображения многоугольник z1 <-- круг z2
    do k = 1,nn 
    z2(k) = exp(dcmplx(0.d0, k-4.d0));   enddo    ! Было k-nn, что было хуже
		tol = 1.d-11			! Точность tol = eps = 10.**(-(nq+1))
		iguess=1				! Начальное приближение задается в SCSOLV
!==============================================================================
! Решение задачи нахождения параметров интеграла Кристоффеля-Шварца SC
     call scsolv(ip, iguess, tol, errest, nn, c, z2, z10, z1, betam,nq,qwork)

! Емкость всей структуры в воздухе. Исп. GHIONE.
!	z1(k)= w1(k) - вершины многоугольника (в оригинале обозначено как w(k))
!	z2(k)= z1(k) - круговая канон.область (в оригинале обозначено z(k))
!	z3(k)и x3(k) - верх.полуплоскость z3 и её веществ.ось x3
!		   x4(k) - веществ.ось x4, где все точки нормированы в диапазон -1..1
!		   х5(k) - точки на вещест.оси х5, соответ. краям полосок МПЛ (нормир.: -1..1)
!------------------------------------------------------------------------------
! Отображение круга z2 на верхнюю полуплоскость (ВПП) z3
    z30 = zi; ! задание конформного центра 
    z301=-zi;
! Точки вещест.оси х3 верх.полуплоск. z3, соответ. всем точкам исх.многоугол. z1 (ненормир.)
    do k = 1, nn-2
    z3(k)=(z2(k)*z301 - z30)/(z2(k) - 1.) ! Послед. тчка многоугол. z1(last)-> z2=1-> z3->oo 
    x3(k)=dreal(z3(k)); enddo
! Отображение ВПП z3 на ВПП z4. Ось х3 -> x4. Нормировка
! Точки веществ.оси х4, соответствующие всем точкам исх.многоугол. области z1 (нормир.:-1..1)
	do k = 1,nn-2
	x4(k) = 2 * (x3(k) - x3(1)) / (x3(nn-2) - x3(1)) - 1.; enddo
! Перенумерация точек оси х4 -> x5
! Точки вещественной оси х5, соответствующие лишь краям полосок МПЛ (нормир.: -1..1)
    do i=1,n
        i4=4*(i-1);			i8=8*(i-1);
        x5(i4+1)=x4(i8+1);	x5(i4+3)=x4(i8+7);
        x5(i4+2)=x4(i8+2);	x5(i4+4)=x4(i8+8);
    enddo
!------------------------------------------------------------------------------
! Кол-во проводников n2 из-за разрезания больше кол-ва линий n, а именно n2 = 2n-1. 
! Кол-во координат краевых точек всех проводников будет 2N+2 = 2(2n-1)+2 = 4n (справ.).

!	if(ip==0) then
!	write(*,'('' z2('',i2,'') ='',2f8.4)')(i, z2(i),i=1,nn);   print*
!	write(*,'('' z3('',i2,'') ='',2f8.4)')(i, z3(i),i=1,nn-2); print*
!	write(*,'('' x4('',i2,'') ='', f8.4)')(i, x4(i),i=1,nn-2); print*
!	write(*,'('' x5('',i2,'') ='', f8.4)')(i, x5(i),i=1,4*n);  endif
!------------------------------------------------------------------------------ 
		M=1000
		n2=2*n-1
! Расчет МПЛ, преобразованных в КПЛ с избыточ. проводниками (из-за разрезания) aC
    call GHIONE(x5,aC,n2,M) 
    call refor(aC,bC,n) ! Удален. избыт.провод. Норм.ёмкость всей МПЛ-структуры bC (б/р)
	if(ip==0) then; print*,'Capacitance matrix [C]';  call DPRINT(bC, n); endif

!==============================================================================
! Расчет МПЛ на диэлектрической полосе xs=tanh(aws)
		aws(1)=0
		aws(2)=aw(1)
		do i=1,n-1
        aws(2*i+1)=aws(2*i) + s(i)
		aws(2*i+2)=aws(2*i+1)+aw(i+1); enddo
		aws=aws-aws(2*n)/2.
	xs(1)=-1.
		do k=1,2*n
		xs(k+1)=tanh(pi*aws(k)/(2*h)); enddo
	xs(2*n+2)=1.

	M=1500
! Нормир.ёмкость МПЛ->КПЛ-структуры на диэл.полосе dC (б/р)
    call GHIONE(xs,dC,n,M) 
	if(ip==0) then; print*,'Capacitance matrix [Cd]';  call DPRINT(dC, n); endif

 	dC = 8.854*( e2*bC + (e1-e2)*dC) ! Итог. матр.емк.(bC+dC)-> dC (пФ/м). (e1>e2)
!================================================================================
    call induc(bC,dL,n)     ! Результир. матрица индуктивностей bC -> dL (мкГн/м)
	return
	end